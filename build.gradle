import org.eclipse.jgit.api.Git
import org.eclipse.jgit.errors.RepositoryNotFoundException

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.1"
        classpath 'org.eclipse.jgit:org.eclipse.jgit:5.10.0.202012080955-r'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: "com.gradle.plugin-publish"

project.ext {
    GIT_INFO = gitInfo(rootProject.file('.'))
    OLD_FORMAT = '2.1-SNAPSHOT'
}

group = 'net.minecraftforge.gradle'
version = "1.1.0"
archivesBaseName = 'ForgeGradle'
targetCompatibility = '8'
sourceCompatibility = '8'


repositories {
    mavenLocal()
    maven {
        name = "forge"
        url = "https://maven.minecraftforge.net"
    }
    maven {
        // because Srg2Source needs an eclipse dependency.
        name = "eclipse"
        url = "https://repo.eclipse.org/content/groups/eclipse/"
    }
    mavenCentral()

    // because SS and its snapshot
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }


    // because of the GradleStart stuff
    maven {
        name = "mojang"
        url = "https://libraries.minecraft.net/"
    }
}

configurations {
    deployerJars
    shade
    compileOnly.extendsFrom shade
}

dependencies {
    implementation gradleApi()

    // moved to the beginning to be the overrider
    implementation 'org.ow2.asm:asm-debug-all:5.0.3'
    implementation 'com.google.guava:guava:18.0'

    implementation 'net.sf.opencsv:opencsv:2.3' // reading CSVs.. also used by SpecialSource
    implementation 'com.cloudbees:diff4j:1.1' // for difing and patching
    implementation 'com.github.abrarsyed.jastyle:jAstyle:1.3' // formatting
    implementation 'net.sf.trove4j:trove4j:2.1.0' // because its awesome.

    implementation 'com.github.jponge:lzma-java:1.3' // replaces the LZMA binary
    implementation 'com.nothome:javaxdelta:2.0.1' // GDIFF implementation for BinPatches
    implementation 'com.google.code.gson:gson:2.2.4' // Used instead of Argo for buuilding changelog.
    implementation 'com.github.tony19:named-regexp:0.2.3' // 1.7 Named regexp features
    implementation 'net.minecraftforge:fernflower:2.0-SNAPSHOT' // Fernflower Forge edition

    shade 'net.md-5:SpecialSource:1.7.4' // deobf and reobs
    // compile 'net.md-5:SpecialSource:1.7.4' // when md5 publishes

    // because curse
    implementation 'org.apache.httpcomponents:httpclient:4.3.3'
    implementation 'org.apache.httpcomponents:httpmime:4.3.3'

    // mcp stuff
    shade 'de.oceanlabs.mcp:RetroGuard:3.6.6'
    shade 'de.oceanlabs.mcp:mcinjector:3.4-SNAPSHOT'
    shade 'net.minecraftforge.srg2source:Srg2Source:3.3-SNAPSHOT'

    // The old version used by S2S is gone, so we need to force the latest build of that minor release
    shade 'org.eclipse.jdt:org.eclipse.jdt.core:3.10.0.+'

    //Stuff used in the GradleStart classes
    compileOnly 'com.mojang:authlib:1.5.16'
    compileOnly "net.minecraft:launchwrapper:1.11"

    testImplementation 'junit:junit:4.12'
}

sourceSets {
    main.compileClasspath += configurations.shade;
    main.runtimeClasspath += configurations.shade;
    test.compileClasspath += configurations.shade;
    test.runtimeClasspath += configurations.shade;
}

compileJava {
	options.deprecation = true
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.WARN
    from(sourceSets.main.resources.srcDirs) {
        include 'forgegradle.version.txt'
        expand 'version': project.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'forgegradle.version.txt'
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.WARN;
    configurations.shade.each { dep ->
        from(project.zipTree(dep)){
            exclude 'META-INF', 'META-INF/**'
        }
    }

    manifest {
        attributes 'version':project.version
        attributes 'javaCompliance': project.targetCompatibility
        attributes 'group':project.group
        attributes 'Implementation-Version': project.version + "-" + GIT_INFO.hash
    }
}

javadoc {
	classpath += configurations.compileOnly

    // linked javadoc urls.. why not...
    options.addStringOption 'link', 'https://gradle.org/docs/current/javadoc/'
    options.addStringOption 'link', 'https://docs.guava-libraries.googlecode.com/git-history/v18.0/javadoc'
    options.addStringOption 'link', 'https://asm.ow2.org/asm50/javadoc/user/'
}

artifacts {
    archives jar
    //archives javadocJar
}

test {
    // These tests are outdated, and I can't be arsed to fix them
    exclude "*"
    // if (project.hasProperty("filesmaven")) // disable this test when on the forge jenkins
    //     exclude "**/ExtensionMcpMappingTest*"
}

pluginBundle {
    website = 'https://www.gradle.org/'
    vcsUrl = 'https://github.com/thefightagainstmalware/ForgeGradle'
    description = 'Gradle plugin for all Minecraft mod development needs'
    tags = ['forge', 'minecraft', 'minecraftforge', 'sponge', 'mcp']

    plugins {
        patcher {
            id = 'net.minecraftforge.gradle.patcher'
            displayName = 'Minicraft Patcher Plugin'
        }
        tweakerClient {
            id = 'net.minecraftforge.gradle.tweaker-client'
            displayName = 'Mincraft Client Tweaker Plugin'
        }
        tweakerServer {
            id = 'net.minecraftforge.gradle.tweaker-server'
            displayName = 'Mincraft Server Tweaker Plugin'
        }
        forge {
            id = 'net.minecraftforge.gradle.forge'
            displayName = 'MincraftForge Mod Development Plugin'
        }

        launch4j {
            id = 'net.minecraftforge.gradle.launch4j'
            displayName = 'Specialized Launch4J Gradle Plugin'
        }
    }
}

// write out version so its convenient for doc deployment
file('build').mkdirs()
file('build/version.txt').text = version;

def gitInfo(dir) {
    String.metaClass.rsplit = { String del, int limit = -1 ->
        def lst = new ArrayList()
        def x = 0, idx
        def tmp = delegate
        while ((idx = tmp.lastIndexOf(del)) != -1 && (limit == -1 || x++ < limit)) {
            lst.add(0, tmp.substring(idx + del.length(), tmp.length()))
            tmp = tmp.substring(0, idx)
        }
        lst.add(0, tmp)
        return lst
    }

    def git = null
    try {
        git = Git.open(dir)
    } catch (RepositoryNotFoundException e) {
        return [
                tag: '0.0',
                offset: '0',
                hash: '00000000',
                branch: 'master',
                commit: '0000000000000000000000',
                abbreviatedId: '00000000'
        ]
    }
    def value = git.describe().setLong(true).setTags(true).call();
    
    if (value == null) {
        return [
                tag: '0.0',
                offset: '0',
                hash: '00000000',
                branch: 'master',
                commit: '0000000000000000000000',
                abbreviatedId: '00000000'
        ]
    }

    def head = git.repository.exactRef('HEAD')
    def longBranch = head.symbolic ? head?.target?.name : null // matches Repository.getFullBranch() but returning null when on a detached HEAD

    def ret = [:]
    ret.tag = desc[0]
    ret.offset = desc[1]
    ret.hash = desc[2]
    ret.branch = longBranch != null ? org.eclipse.jgit.lib.Repository.shortenRefName(longBranch) : null
    ret.commit = org.eclipse.jgit.lib.ObjectId.toString(head.objectId)
    ret.abbreviatedId = head.objectId.abbreviate(8).name()

    return ret
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = 'ForgeGradle'
                description = 'A concise description of my library'
                url = 'https://github.com/thefightagainstmalware/ForgeGradle/'
                licenses {
                    license {
                        name = '                  GNU LESSER GENERAL PUBLIC LICENSE\n' +
                                '                       Version 2.1'
                        url = 'https://raw.githubusercontent.com/thefightagainstmalware/ForgeGradle/nosession-main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'forgedevs'
                        name = 'Forge Developers'
                    }

                    developer {
                        id = 'pandaninjas'
                        name = "PandaNinjas"
                        email = "admin@malwarefight.wip.la"
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/thefightagainstmalware/ForgeGradle'
                    developerConnection = 'scm:git:https://github.com/thefightagainstmalware/ForgeGradle'
                    url = 'https://github.com/thefightagainstmalware/ForgeGradle/'
                }
            }
        }
    }

}

